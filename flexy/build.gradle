plugins {
    id 'java'
    id 'application'
}

group = 'com.prashik.flexy'
version = '1.0.0'
description = """Backend-helidon-server"""

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    helidonversion = '4.0.10'
    mainClass='com.prashik.flexy.Main'
}

dependencies {
    implementation enforcedPlatform("io.helidon:helidon-dependencies:${project.helidonversion}")
    implementation 'io.helidon.webserver:helidon-webserver'
    implementation 'io.helidon.http.media:helidon-http-media-jsonp'
    implementation 'io.helidon.config:helidon-config-yaml'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task copyLibs(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/libs'
}

copyLibs.dependsOn jar
assemble.dependsOn copyLibs

jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes ('Main-Class': "${project.mainClass}",
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}

application {
    mainClass = "${project.mainClass}"
}
